######################################################################
# Communi
######################################################################

# qmake vs. environment variables
isEmpty(COMMUNI_INSTALL_PREFIX): \
    COMMUNI_INSTALL_PREFIX = $$(COMMUNI_INSTALL_PREFIX)
isEmpty(COMMUNI_INSTALL_BINS): \
    COMMUNI_INSTALL_BINS = $$(COMMUNI_INSTALL_BINS)
isEmpty(COMMUNI_INSTALL_LIBS): \
    COMMUNI_INSTALL_LIBS = $$(COMMUNI_INSTALL_LIBS)
isEmpty(COMMUNI_INSTALL_PLUGINS): \
    COMMUNI_INSTALL_PLUGINS = $$(COMMUNI_INSTALL_PLUGINS)
isEmpty(COMMUNI_INSTALL_ICONS): \
    COMMUNI_INSTALL_ICONS = $$(COMMUNI_INSTALL_ICONS)
isEmpty(COMMUNI_INSTALL_DESKTOP): \
    COMMUNI_INSTALL_DESKTOP = $$(COMMUNI_INSTALL_DESKTOP)
isEmpty(COMMUNI_INSTALL_THEMES): \
    COMMUNI_INSTALL_THEMES = $$(COMMUNI_INSTALL_THEMES)

isEmpty(COMMUNI_INSTALL_BINS) {
    !isEmpty(COMMUNI_INSTALL_PREFIX):COMMUNI_INSTALL_BINS = $$COMMUNI_INSTALL_PREFIX/bin
    else:win32:COMMUNI_INSTALL_BINS = $$[QT_INSTALL_BINS]
    else:mac:COMMUNI_INSTALL_BINS = /Applications
    else:COMMUNI_INSTALL_BINS = /usr/bin
}

isEmpty(COMMUNI_INSTALL_LIBS) {
    !isEmpty(COMMUNI_INSTALL_PREFIX):COMMUNI_INSTALL_LIBS = $$COMMUNI_INSTALL_PREFIX/lib
    else:win32:COMMUNI_INSTALL_LIBS = $$[QT_INSTALL_LIBS]
    else:mac:COMMUNI_INSTALL_LIBS = /Library/Frameworks
    else:COMMUNI_INSTALL_LIBS = /usr/lib
}

isEmpty(COMMUNI_INSTALL_PLUGINS) {
    !isEmpty(COMMUNI_INSTALL_PREFIX):COMMUNI_INSTALL_PLUGINS = $$COMMUNI_INSTALL_PREFIX/plugins
    else:COMMUNI_INSTALL_PLUGINS = $$[QT_INSTALL_PLUGINS]/communi
}

isEmpty(COMMUNI_INSTALL_ICONS) {
    !isEmpty(COMMUNI_INSTALL_PREFIX):COMMUNI_INSTALL_ICONS = $$COMMUNI_INSTALL_PREFIX/icons
    else:COMMUNI_INSTALL_ICONS = /usr/share/icons/hicolor
}

isEmpty(COMMUNI_INSTALL_DESKTOP) {
    !isEmpty(COMMUNI_INSTALL_PREFIX):COMMUNI_INSTALL_DESKTOP = $$COMMUNI_INSTALL_PREFIX
    else:COMMUNI_INSTALL_DESKTOP = /usr/share/applications
}

isEmpty(COMMUNI_INSTALL_THEMES) {
    !isEmpty(COMMUNI_INSTALL_PREFIX):COMMUNI_INSTALL_THEMES = $$COMMUNI_INSTALL_PREFIX/themes
    else:win32:COMMUNI_INSTALL_THEMES = $$[QT_INSTALL_BINS]/themes
    else:COMMUNI_INSTALL_THEMES = /usr/share/themes/communi
}
